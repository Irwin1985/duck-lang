<program> ::= <stmtlist>
<stmtlist> ::= <stmt> <stmtlist>
<stmtlist> ::= <epsilon>
<identifier-list> ::= <identifier> , <identifier-list>
<identifier-list> ::= <identifier>
<stmt> ::= import <identifier-list> <endl>
<stmt> ::= require <identifier-list> <endl>
<stmt> ::= include <string> <endl>
<stmt> ::= <expr> <endl>
<stmt> ::= <endl>
<stmt> ::= global <identifier-list>
<stmt> ::= local <identifier-list>
<stmt> ::= <assignment> <endl>
<stmt> ::= global <assignment> <endl>
<stmt> ::= local <assignment> <endl>
<stmt> ::= static <assignment> <endl>
<stmt> ::= <self-assignment> <endl>
<stmt> ::= <function-def> <endl>
<stmt> ::= global <function-def> <endl>
<stmt> ::= local <function-def> <endl>
<stmt> ::= static <function-def> <endl>
<stmt> ::= <if> <endl>
<stmt> ::= <for-loop> <endl>
<stmt> ::= <for-each> <endl>
<stmt> ::= <while-loop> <endl>
<stmt> ::= <do-while> <endl>
<stmt> ::= <let-block> <endl>
<stmt> ::= <try-block> <endl>
<stmt> ::= <class> <endl>
<stmt> ::= return <endl>
<stmt> ::= return <expr> <endl>
<stmt> ::= break <endl>
<stmt> ::= continue <endl>
<stmt> ::= throw <expr> <endl>
<stmt> ::= call \ cc <reference> <endl>
<assignment> ::= <l-value> = <condition>
<self-assignment> ::= <l-value> ++
<self-assignment> ::= <l-value> --
<self-assignment> ::= <l-value> += <arithmetic>
<self-assignment> ::= <l-value> -= <arithmetic>
<self-assignment> ::= <l-value> *= <arithmetic>
<self-assignment> ::= <l-value> /= <arithmetic>
<function-def> ::= func <function-decl>
<function-decl> ::= <identifier> <parameters> <endl> <stmtlist> end
<function-decl> ::= <string> <parameters> <endl> <stmtlist> end
<class> ::= class <identifier> <opt-extends> <parameters> <endl> <stmtlist> end
<parameters> ::= ( )
<parameters> ::= ( <param-decl> )
<parameters> ::= <epsilon>
<param-decl> ::= <identifier>
<param-decl> ::= <identifier> , <param-decl>
<opt-extends> ::= : <identifier-list>
<opt-extends> ::= <epsilon>
<if> ::= if <condition> then <endl> <stmtlist> <elseif>
<elseif> ::= else <endl> <stmtlist> endif
<elseif> ::= else <if>
<elseif> ::= endif
<for-loop> ::= for <identifier> = <arithmetic> to <arithmetic> <step>
<step> ::= step <arithmetic> do <endl> <stmtlist> loop
<step> ::= do <endl> <stmtlist> loop
<for-each> ::= for <identifier> in <expr> do <endl> <stmtlist> loop
<while-loop> ::= while <condition> do <endl> <stmtlist> loop
<do-while> ::= do <endl> <stmtlist> loop while <condition> 
<let-block> ::= let <bindings> begin <endl> <stmtlist> end
<bindings> ::= <identifier> = <expr> , <bindings>
<bindings> ::= <identifier> = <expr> , <endl> <bindings>
<bindings> ::= <identifier> = <expr> 
<bindings> ::= <identifier> = <expr> <endl>
<try-block> ::= try <endl> <stmtlist> <catch-block>
<catch-block> ::= catch <l-value> <endl> <stmtlist> done
<catch-block> ::= done
<l-value> ::= <identifier>
<l-value> ::= ( <l-value> )
<l-value> ::= <reference> . <identifier>
<l-value> ::= <reference> [ <expr> ]
<expr> ::= <condition>
<condition> ::= <condition> and <logic>
<condition> ::= <condition> or <logic>
<condition> ::= <logic>
<logic> ::= not <comparison>
<logic> ::= <comparison>
<comparison> ::= <comparison> == <arithmetic>
<comparison> ::= <comparison> != <arithmetic>
<comparison> ::= <comparison> \< <arithmetic>
<comparison> ::= <comparison> > <arithmetic>
<comparison> ::= <comparison> \<= <arithmetic>
<comparison> ::= <comparison> >= <arithmetic>
<comparison> ::= <final> is <reference>
<comparison> ::= <final> is not <reference>
<comparison> ::= <arithmetic>
<arithmetic> ::= <arithmetic> + <term>
<arithmetic> ::= <arithmetic> - <term>
<arithmetic> ::= <term>
<term> ::= <term> * <factor>
<term> ::= <term> / <factor>
<term> ::= <term> mod <factor>
<term> ::= <factor>
<factor> ::= - <factor>
<factor> ::= ! <factor>
<factor> ::= * <factor>
<factor> ::= & <factor>
<factor> ::= <final>
<final> ::= ( <expr> )
<final> ::= <boolean>
<final> ::= <object>
<final> ::= <reference>
<final> ::= new <reference>
<final> ::= <integer>
<final> ::= <float>
<final> ::= <string>
<reference> ::= <l-value>
<reference> ::= this
<reference> ::= <reference> ( )
<reference> ::= <reference> ( <arguments> )
<arguments> ::= <arguments>, <expr>
<arguments> ::= <expr>
<object> ::= [ ]
<object> ::= <endl> [ <array-begin>
<object> ::= [ <array-begin>
<array-begin> ::= <endl> <expr> <object-array>
<array-begin> ::= <expr> <object-array>
<object-array> ::= , <endl> <expr> <object-array>
<object-array> ::= , <expr> <object-array>
<object-array> ::= <endl> ]
<object-array> ::= ]
<object> ::= <endl> [ <dictionary-begin>
<object> ::= [ <dictionary-begin>
<dictionary-begin> ::= <endl> <identifier> : <expr> <object-dictionary>
<dictionary-begin> ::= <identifier> : <expr> <object-dictionary>
<object-dictionary> ::= , <endl> <identifier> : <expr> <object-dictionary>
<object-dictionary> ::= , <identifier> : <expr> <object-dictionary>
<object-dictionary> ::= <endl> ]
<object-dictionary> ::= ]
<boolean> ::= true
<boolean> ::= false
